apply plugin: 'android'

android {
    compileSdkVersion 23
    buildToolsVersion '23.0.0'

    defaultConfig {
        minSdkVersion 16
        targetSdkVersion 23
        versionCode 83
        versionName "1.0-rc1"
        applicationId "org.sugr.gearshift"
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }
    signingConfigs {
        release
    }
    buildTypes {
        release {
            signingConfig signingConfigs.release
            minifyEnabled true
            // proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.txt'
            proguardFiles 'proguard-rules.txt'
            zipAlignEnabled true
        }
    }
    lintOptions {
        checkReleaseBuilds true
        // Or, if you prefer, you can continue to check for errors in release builds,
        // but continue the build even when errors are found:
        abortOnError false
        disable 'MissingTranslation'
    }

    packagingOptions {
        exclude 'META-INF/ASL2.0'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/NOTICE'
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])

    compile 'com.android.support:support-v4:23.0.+'
    compile 'com.android.support:appcompat-v7:23.0.+'
    compile 'com.android.support:recyclerview-v7:23.0.+'
    compile 'com.android.support:cardview-v7:23.0.+'
    compile 'com.fasterxml.jackson.core:jackson-core:2.3.0'
    compile 'com.fasterxml.jackson.core:jackson-databind:2.3.0'
    compile 'com.fasterxml.jackson.core:jackson-annotations:2.3.0'
    compile 'com.mcxiaoke.volley:library:1.+'

    testCompile 'junit:junit:4.12'
    testCompile "org.mockito:mockito-all:1.10.+"
    testCompile('org.robolectric:robolectric:3.0') {
        exclude group: 'commons-logging', module: 'commons-logging'
        exclude group: 'org.apache.httpcomponents', module: 'httpclient'
    }
}

def runTasks = gradle.startParameter.taskNames
if ('assemble' in runTasks || 'assembleRelease' in runTasks || 'aR' in runTasks) {
    def storePass = System.env.STORE_PASS ?: new String(System.console().readPassword('\nKeyStore Password: '))
    def keyPass = System.env.KEY_PASS ?: new String(System.console().readPassword('Alias Password: '))

    android.signingConfigs.release.storeFile = file('/home/urandom/.android/sugr.keystore')
    android.signingConfigs.release.storePassword = storePass
    android.signingConfigs.release.keyAlias = "gearshift"
    android.signingConfigs.release.keyPassword = keyPass
    android.buildTypes.release.signingConfig = android.signingConfigs.release
}
